city_airport(City, Airport) :-
  city(City),
  airport(Airport, _, City).

airport_country(Airport, Country) :-
  country(Country),
  airport(Airport, Country, _).

flights_from_with_company(Airline, Airport) :-
  flight(Airport, _, _, _, _, Airline). 

direct(From, To) :-
  flight(From, To, _, _, _, _).
  
gen_connection(From, To, Price, Start, End, 1) :-
  flight(From, To, Price, Start, End, _),
  From \= To.
gen_connection(From, To, Price, Start, End, Hops) :-
  flight(From, Mid, P1, Start, En1, _),
  gen_connection(Mid, To, P2, St2, En2, Hs),
  En1 =< St2,
  From \= To,
  count(airport(_), Na),
  Hs < Na, 
  End is En2,
  Hops is Hs+1, 
  Price is P1+P2.

% Note: Start, End must be ground!
possible_connection(From, To, Price, Start, End, Hops) :-
  gen_connection(From, To, Price, St1, En1, Hops),
  Start =< St1,
  En1 =< End.

cheapest_connection(From, To, Price) :-
  airport(From),
  airport(To),
  min(gen_connection(From, To, P, _, _, _), P, Price).
  
min_hop_connection(From, To, Hops) :-
  airport(From),
  airport(To),
  min(gen_connection(From, To, _, _, _, Hs), Hs, Hops).

cheap_flights_to(Destination, Price) :-
  airport(Destination),
  min(flight(_, Destination, P, _, _, _), P, Price).

avg_flight_price(Destination, AvgPrice) :-
  airport(Destination),
  avg(flight(_, Destination, Price, _, _, _), Price, AvgPrice).
